group 'papeeria-compiler-fleet'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.+'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.+'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'distribution'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
    jcenter()
}

distributions {
    main {
        baseName = 'papeeria-compiler-fleet'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.xenomachina:kotlin-argparser:2.+"
    compile "com.typesafe:config:1.3.3"
    compile "com.spotify:docker-client:8.11.+"
    compile "com.google.cloud:google-cloud-pubsub:0.32.0-beta"
    compile 'com.google.protobuf:protobuf-java:3.5.+'
    compile 'commons-io:commons-io:2.+'
    compile 'com.google.guava:guava:23.+'
    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.3.+'
    compile 'ch.qos.logback:logback-core:1.3.+'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.+'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    //uncomment when we start doing grpc requests
    //compile 'io.grpc:grpc-protobuf:1.+'

    compile files('lib/texbe-proto.jar')
}

task cli(type: JavaExec) {
    main = "com.bardsoftware.backend.fleet.rmarkdown.CmdLineClientKt"
    classpath = sourceSets.main.runtimeClasspath

    /* Need to split the space-delimited value in the exec.args */
    args System.getProperty("exec.args", "").split()
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDirs 'src/main/java', 'src/generated/main/java'
        }
    }
}


protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
        generatedFilesBaseDir = "$projectDir/src/generated"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task makeJar(type: Jar) {
    dependsOn 'generateProto'
    dependsOn compileJava
    from ('build/classes/java/main')
    archiveName = 'compiler-fleet-proto.jar'
}

mainClassName = 'com.bardsoftware.backend.fleet.rmarkdown.SubscriberKt'
